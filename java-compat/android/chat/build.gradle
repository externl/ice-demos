// **********************************************************************
//
// Copyright (c) 2003-present ZeroC, Inc. All rights reserved.
//
// **********************************************************************

apply plugin: 'com.android.application'
apply from: "$rootProject.projectDir/../gradle/ice.gradle"

slice {
    java {
        include = ["${projectDir}/src/main/slice"]
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        applicationId "com.zeroc.chat"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'UnusedAttribute'  // autofillHints is only used with API level 26 and older
    }
}
task copyBksTask(type: Copy) {
    from "${rootProject.projectDir}/../../certs/client.bks"
    into "src/main/res/raw"
}
preBuild.dependsOn(copyBksTask)

clean {
    delete("src/main/res/raw/client.bks")
}

dependencies {
    implementation "com.zeroc:ice-compat:${project.iceArtifactVersion}"
    implementation "com.zeroc:glacier2-compat:${project.iceArtifactVersion}"
    implementation "androidx.fragment:fragment:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.2"
}
